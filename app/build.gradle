/*
 * ITAD Android
 * Copyright (C) 2018-present  Kieran O'Brien
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "com.google.gms.oss.licenses.plugin"

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "network.obrien.isthereanydeal"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro",
                    "proguard-retrofit-rules.pro", "proguard-okio-rules.pro"
            resValue "string", "app_id", "${defaultConfig.applicationId}"
            resValue "string", "app_version", "${defaultConfig.versionName}"
        }

        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_id", "${defaultConfig.applicationId}${applicationIdSuffix}"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(include: ["*.jar"], dir: "libs")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"

    // Anko
    implementation "org.jetbrains.anko:anko:${versions.anko}"

    // Third party licensing
    debugImplementation "com.google.android.gms:play-services-oss-licenses:${versions.play_services_oss_licenses}"

    // Support
    implementation "com.android.support:appcompat-v7:${versions.android_support.appcompat_v7}"
    implementation "com.android.support.constraint:constraint-layout:${versions.android_support.constraint_layout}"
    implementation "com.android.support:design:${versions.android_support.design}"
    implementation "com.android.support:preference-v7:${versions.android_support.preference_v7}"
    implementation "com.android.support:preference-v14:${versions.android_support.preference_v14}"
    implementation "com.android.support:support-v4:${versions.android_support.support_v4}"

    // Timber
    implementation "com.jakewharton.timber:timber:${versions.timber}"

    // Dagger
    implementation "com.google.dagger:dagger:${versions.dagger}"
    implementation "com.google.dagger:dagger-android:${versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${versions.dagger}"
    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${versions.dagger}"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxjava}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxandroid}"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"

    // Testing
    testImplementation "junit:junit:${versions.junit}"
    androidTestImplementation "com.android.support.test:runner:${versions.android_support.test_runner}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${versions.android_support.espresso}"
}
