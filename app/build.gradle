/*
 * IsThereAnyDeal Android
 * Copyright (C) 2018-present  Kieran O'Brien
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "com.google.gms.oss.licenses.plugin"

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "network.obrien.isthereanydeal"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro",
                    "proguard-retrofit-rules.pro", "proguard-okio-rules.pro"
            resValue "string", "app_id", "${defaultConfig.applicationId}"
            resValue "string", "app_version", "${defaultConfig.versionName}"
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            resValue "string", "app_id", "${defaultConfig.applicationId}${applicationIdSuffix}"
            resValue "string", "app_version", "${defaultConfig.versionName}${applicationIdSuffix}"
        }
    }
    dataBinding {
        enabled = true
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Anko
    def anko_version = "0.10.5"
    implementation "org.jetbrains.anko:anko:$anko_version"

    // Third party licensing
    def play_services_oss_licenses_version = "15.0.1"
    implementation "com.google.android.gms:play-services-oss-licenses:$play_services_oss_licenses_version"

    // Support
    def appcompat_version = "27.1.1"
    implementation "com.android.support:support-v4:$appcompat_version"
    implementation "com.android.support:appcompat-v7:$appcompat_version"
    implementation "com.android.support:preference-v7:$appcompat_version"
    implementation "com.android.support:preference-v14:$appcompat_version"
    implementation "com.android.support:design:$appcompat_version"

    // Constraint layout
    def constraint_layout_version = "1.1.0"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    // Timber
    def timber_version = "4.7.0"
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Dagger
    def dagger_version = "2.16"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // RxJava
    def rxjava_version = "2.1.13"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"

    def rxandroid_version = "2.0.2"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // Retrofit
    def retrofit_version = "2.4.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    def okhttp_version = "3.10.0"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Testing
    def junit_version = "4.12"
    testImplementation "junit:junit:$junit_version"

    def runner_version = "1.0.2"
    androidTestImplementation "com.android.support.test:runner:$runner_version"

    def espresso_version = "3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
}
